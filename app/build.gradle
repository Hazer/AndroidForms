buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.1.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "0.1"

repositories {
    jcenter()
    mavenCentral()
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"
    resourcePrefix "androidforms__"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName version
    }
    buildTypes {
    }
}

def siteUrl = 'https://github.com/Exallium/AndroidForms'
def gitUrl = 'https://github.com/Exallium/AndroidForms.git'
group = 'com.exallium.AndroidForms'

install {
    repositories.mavenInstaller {
        // Generate POM.xml
        pom {
            project {
                packaging 'aar'
                name 'Android Forms'
                url siteUrl
                
                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    testCompile 'org.apache.maven:maven-ant-tasks:2.1.3'
    testCompile "org.mockito:mockito-core:1.9.5"
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = "sources"
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options {
        links("http://docs.oracle.com/javase/7/docs/api/");
        linksOffline("http://d.android.com/reference", "${android.sdkDirectory}/docs/reference");
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

bintray {
    user = mavenUser
    key = binTrayApiKey
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = "com.exallium.AndroidForms"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["MIT"]
        publish = true
    }
}
